# --------------------------------------------------------------------------- #
# The Toeplitz Project Literature Library                                     #
# Copyright (c) 2011-2022 Toeplitz Project contributors                       #
#                                                                             #
# This Source Code Form is subject to the terms of the Mozilla Public         #
# License, v. 2.0. If a copy of the MPL was not distributed with this         #
# file, You can obtain one at http://mozilla.org/MPL/2.0/.                    #
# --------------------------------------------------------------------------- #

from ..rdefs import *

deduc Thm with d_zeta, d_lam, d_frl {
    intr I {
        sy="Let $@alp in bbZ[zet]$."
    }
    supp S {
        sy="$@frl not mid alp$"
    }
    exis E {
        intr I {
            sy="$@r in bbZ$"
        }
        asrt A {
            en="
            $@zetuur alp$ is semiprimary.
            "
        }
    }
    meson="Let I. Suppose S. Then E."
}

deduc Pf of Thm.E {
    intr I10 {
        en="
        Write
            $@alp equiv a + b qnt 1 - zet; mod frl squ$
        where $a$ and $b$ are rational integers.
        "
    }
    exis E20 {
        intr I {
            sy="$@b star in bbZ$"
        }
        asrt A {
        sy="$@a b star equiv b mod ell$"
        }
    }
    asrt A30 {
        sy="$@zet sup b star; alp equiv a mod frl squ$"
    }
    asrt A40 {
        en="$@zet sup b star; alp$ is semiprimary."
    }
    meson="Let I10. Now E20 by Thm.S. Then A30, so A40. Hence Thm.E."
}
